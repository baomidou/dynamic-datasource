// 编译脚本
buildscript {
    ext {
        // Spring Boot 版本定义 - 按子模块适配
        springBoot2Version = "2.7.18"  // Spring Boot 2.x 最新稳定版，JDK 8+
        springBoot3Version = "3.5.7"   // Spring Boot 3.x，JDK 17+
        springBoot4Version = "4.0.0"   // Spring Boot 4.x，JDK 17+

        // 第三方库版本
        mybatisPlusVersion = "3.5.14"
        mybatisSpringVersion = "2.1.2"
        p6spyVersion = "3.9.1"
        seataVersion = "1.4.2"
        h2Version = "2.2.224"
        commonsDbcp2Version = "2.10.0"
        atomikosVersion = "4.0.6"
        oracleUcpVersion = "23.4.0"
        hikaricpVersion = "2.4.13"
        beeCpVersion = "3.4.2"
        c3p0Version = "0.10.1"
        druidVersion = "1.2.27"
    }
    // 仓库配置
    repositories {
        mavenLocal()
        maven { url = 'https://maven.aliyun.com/repository/central' }
        maven { url = 'https://maven.aliyun.com/repository/spring' }
        maven { url = 'https://maven.aliyun.com/repository/google' }
        mavenCentral()
    }

    dependencies {
        classpath("io.spring.dependency-management:io.spring.dependency-management.gradle.plugin:1.1.7")
        // gradle-maven-publish-plugin 0.32.0 要求 JDK 11+，只在 JDK 11+ 环境下加载
        if (JavaVersion.current() >= JavaVersion.VERSION_11) {
            classpath("com.vanniktech:gradle-maven-publish-plugin:0.32.0")
        }
    }
}

description = "mybatis dynamic datasource"

// 项目配置
allprojects {
    group = APP_GROUP
    version = APP_VERSION
}

// 子模块配置
subprojects {
    apply plugin: 'java-library'

    // 只在 JDK 11+ 环境下应用发布插件 (gradle-maven-publish-plugin 0.32.0 要求 JDK 11+)
    // JDK 8 环境下仅运行测试，无需发布功能
    if (JavaVersion.current() >= JavaVersion.VERSION_11) {
        apply plugin: 'com.vanniktech.maven.publish'
        apply plugin: 'signing'
    }

    // 仓库配置
    repositories {
        mavenLocal()
        maven { url = 'https://maven.aliyun.com/repository/central' }
        maven { url = 'https://maven.aliyun.com/repository/spring' }
        maven { url = 'https://maven.aliyun.com/repository/google' }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenCentral()
    }

    // 依赖配置
    dependencies {
        // 日志
        api("org.slf4j:slf4j-api")

        // lombok
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
        testAnnotationProcessor("org.projectlombok:lombok")
        testCompileOnly("org.projectlombok:lombok")
    }

    // 默认编译环境 JDK-1.8+ (子模块可覆盖)
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.warnings = false
        options.deprecation = true
        options.compilerArgs += ["-parameters", "-Xlint:unchecked", "-Xlint:-serial"]
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    tasks.register('sourcesJar', Jar) {
        dependsOn classes
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc {
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
            failOnError false
            links "https://docs.oracle.com/javase/8/docs/api"
        }
    }

    tasks.withType(MavenPublication).configureEach {
        doFirst {
            options.skipCertificateChecks = true
        }
    }

    tasks.register('javadocJar', Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }
}
